plugins {
    alias(libs.plugins.android.library)
    id 'maven-publish'
}

group "com.github.martinkade"

android {
    namespace "com.github.barteksc.pdfviewer"

    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.targetSdk.get().toInteger()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation libs.androidx.appcompat
    api libs.pdfium.android
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'com.github.martinkade'
            artifactId = 'android-pdf-viewer'
            version = libs.versions.versionName.get()

            afterEvaluate {
                from components.release
            }
        }
    }
}

tasks.register('generateRepo') {
    group = 'publishing'
    description = 'Generates a local Maven repository'
    dependsOn 'publishReleasePublicationToMyRepoRepository'
}
